// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import {BLS} from "lib/bls-solidity/src/libraries/BLS.sol";

/// @title DrandOracle
/// @author Lui Yong Sheng (@luiyongsheng)
/// @notice On-chain oracle for verifying drand evmnet randomness beacons (BN254).
/// @dev Verifies BLS signatures over BN254 using the EVM pairing precompile.
///      Anyone can submit a valid drand round signature; once verified, the
///      derived randomness (sha256 of the signature) is stored on-chain.
contract DrandOracle {
    /// @notice The most recently verified round number.
    uint64 public latestRound;

    /// @notice The randomness value for the most recently verified round.
    bytes32 public latestRandomness;

    /// @notice Mapping from round number to its verified randomness value.
    mapping(uint64 => bytes32) public roundRandomness;

    /// @notice Domain Separation Tag used by drand evmnet for BN254 BLS signatures.
    string public constant DST = "BLS_SIG_BN254G1_XMD:KECCAK-256_SVDW_RO_NUL_";

    /// @notice Returns the drand evmnet BN254 public key as a G2 point.
    function PUBLIC_KEY() public pure returns (BLS.PointG2 memory) {
        return BLS.PointG2(
            [
                0x0557ec32c2ad488e4d4f6008f89a346f18492092ccc0d594610de2732c8b808f,
                0x07e1d1d335df83fa98462005690372c643340060d205306a9aa8106b6bd0b382
            ],
            [
                0x297d3a4f9749b33eb2d904c9d9ebf17224150ddd7abd7567a9bec6c74480ee0b,
                0x0095685ae3a85ba243747b1b2f426049010f6b73a0cf1d389351d5aaaa1047f6
            ]
        );
    }

    /// @notice Emitted when a new drand round is successfully verified and stored.
    /// @param round The verified round number.
    /// @param randomness The sha256 hash of the round's BLS signature.
    event RoundUpdated(uint64 indexed round, bytes32 randomness);

    /// @notice Verifies a drand BLS signature and stores the derived randomness.
    /// @dev The signature is verified against the drand evmnet public key using
    ///      the BN254 pairing precompile. Only rounds newer than the current
    ///      latest round are accepted.
    /// @param round The drand round number.
    /// @param signature The 64-byte uncompressed BLS G1 signature for the round.
    function updateRound(uint64 round, bytes calldata signature) external {
        require(round > latestRound, "Stale round");

        (bool callSuccess, bool pairingSuccess) = BLS.verifySingle(
            BLS.g1Unmarshal(signature),
            PUBLIC_KEY(),
            BLS.hashToPoint(bytes(DST), abi.encodePacked(keccak256(abi.encodePacked(round))))
        );
        require(callSuccess && pairingSuccess, "Invalid Drand Signature");

        bytes32 randomness = sha256(signature);

        latestRound = round;
        latestRandomness = randomness;
        roundRandomness[round] = randomness;

        emit RoundUpdated(round, randomness);
    }

    /// @notice Returns the latest verified round number and its randomness.
    /// @return round The latest verified round number.
    /// @return randomness The sha256-derived randomness for that round.
    function getLatestRandomness() external view returns (uint64 round, bytes32 randomness) {
        round = latestRound;
        require(round > 0, "No randomness generated yet");
        randomness = latestRandomness;
    }
}
